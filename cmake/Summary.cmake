################################################################################################
# Deep status report function.
# Automatically align right column and selects text based on condition.
# Usage:
#   deep_status(<text>)
#   deep_status(<heading> <value1> [<value2> ...])
#   deep_status(<heading> <condition> THEN <text for TRUE> ELSE <text for FALSE> )
function(deep_status text)
  set(status_cond)
  set(status_then)
  set(status_else)

  set(status_current_name "cond")
  foreach(arg ${ARGN})
    if(arg STREQUAL "THEN")
      set(status_current_name "then")
    elseif(arg STREQUAL "ELSE")
      set(status_current_name "else")
    else()
      list(APPEND status_${status_current_name} ${arg})
    endif()
  endforeach()

  if(DEFINED status_cond)
    set(status_placeholder_length 23)
    string(RANDOM LENGTH ${status_placeholder_length} ALPHABET " " status_placeholder)
    string(LENGTH "${text}" status_text_length)
    if(status_text_length LESS status_placeholder_length)
      string(SUBSTRING "${text}${status_placeholder}" 0 ${status_placeholder_length} status_text)
    elseif(DEFINED status_then OR DEFINED status_else)
      message(STATUS "${text}")
      set(status_text "${status_placeholder}")
    else()
      set(status_text "${text}")
    endif()

    if(DEFINED status_then OR DEFINED status_else)
      if(${status_cond})
        string(REPLACE ";" " " status_then "${status_then}")
        string(REGEX REPLACE "^[ \t]+" "" status_then "${status_then}")
        message(STATUS "${status_text} ${status_then}")
      else()
        string(REPLACE ";" " " status_else "${status_else}")
        string(REGEX REPLACE "^[ \t]+" "" status_else "${status_else}")
        message(STATUS "${status_text} ${status_else}")
      endif()
    else()
      string(REPLACE ";" " " status_cond "${status_cond}")
      string(REGEX REPLACE "^[ \t]+" "" status_cond "${status_cond}")
      message(STATUS "${status_text} ${status_cond}")
    endif()
  else()
    message(STATUS "${text}")
  endif()
endfunction()


################################################################################################
# Function for fetching Deep version from git and headers
# Usage:
#   deep_extract_deep_version()
function(deep_extract_deep_version)
  set(Deep_GIT_VERSION "unknown")
  find_package(Git)
  if(GIT_FOUND)
    execute_process(COMMAND ${GIT_EXECUTABLE} describe --tags --always --dirty
                    ERROR_QUIET OUTPUT_STRIP_TRAILING_WHITESPACE
                    WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}"
                    OUTPUT_VARIABLE Deep_GIT_VERSION
                    RESULT_VARIABLE __git_result)
    if(NOT ${__git_result} EQUAL 0)
      set(Deep_GIT_VERSION "unknown")
    endif()
  endif()

  set(Deep_GIT_VERSION ${Deep_GIT_VERSION} PARENT_SCOPE)
  set(Deep_VERSION "<TODO> (Deep doesn't declare its version in headers)" PARENT_SCOPE)

  # deep_parse_header(${Deep_INCLUDE_DIR}/deep/version.hpp Deep_VERSION_LINES DEEP_MAJOR DEEP_MINOR DEEP_PATCH)
  # set(Deep_VERSION "${DEEP_MAJOR}.${DEEP_MINOR}.${DEEP_PATCH}" PARENT_SCOPE)

  # or for #define Deep_VERSION "x.x.x"
  # deep_parse_header_single_define(Deep ${Deep_INCLUDE_DIR}/deep/version.hpp Deep_VERSION)
  # set(Deep_VERSION ${Deep_VERSION_STRING} PARENT_SCOPE)

endfunction()


################################################################################################
# Prints accumulated deep configuration summary
# Usage:
#   deep_print_configuration_summary()

function(deep_print_configuration_summary)
  deep_extract_deep_version()
  set(Deep_VERSION ${Deep_VERSION} PARENT_SCOPE)

  deep_merge_flag_lists(__flags_rel CMAKE_CXX_FLAGS_RELEASE CMAKE_CXX_FLAGS)
  deep_merge_flag_lists(__flags_deb CMAKE_CXX_FLAGS_DEBUG   CMAKE_CXX_FLAGS)

  deep_status("")
  deep_status("******************* Deep Configuration Summary *******************")
  deep_status("General:")
  deep_status("  Version           :   ${DEEP_TARGET_VERSION}")
  deep_status("  Git               :   ${Deep_GIT_VERSION}")
  deep_status("  System            :   ${CMAKE_SYSTEM_NAME}")
  deep_status("  C++ compiler      :   ${CMAKE_CXX_COMPILER}")
  deep_status("  Release CXX flags :   ${__flags_rel}")
  deep_status("  Debug CXX flags   :   ${__flags_deb}")
  deep_status("  Build type        :   ${CMAKE_BUILD_TYPE}")
  deep_status("")
  deep_status("  BUILD_SHARED_LIBS :   ${BUILD_SHARED_LIBS}")
  deep_status("  BUILD_python      :   ${BUILD_python}")
  deep_status("  BUILD_matlab      :   ${BUILD_matlab}")
  deep_status("  BUILD_docs        :   ${BUILD_docs}")
  deep_status("  CPU_ONLY          :   ${CPU_ONLY}")
  deep_status("  USE_OPENCV        :   ${USE_OPENCV}")
  deep_status("  USE_LEVELDB       :   ${USE_LEVELDB}")
  deep_status("  USE_LMDB          :   ${USE_LMDB}")
  deep_status("  USE_NCCL          :   ${USE_NCCL}")
  deep_status("  ALLOW_LMDB_NOLOCK :   ${ALLOW_LMDB_NOLOCK}")
  deep_status("")
  deep_status("Information:")
  deep_status("  include          : ${CMAKE_INCLUDE_PATH}" )
  get_property(dirs DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY INCLUDE_DIRECTORIES)
  foreach(dir ${dirs})
      message(STATUS "include dir='${dir}'")
  endforeach()
  deep_status("  link          : ${CMAKE_LIBRARY_PATH}" )
  get_property(dirs DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY LINK_DIRECTORIES)
  foreach(dir ${dirs})
      message(STATUS "link dir='${dir}'")
  endforeach()
  deep_status("")
  deep_status("Dependencies:")
  deep_status("  BLAS              : " APPLE THEN "Yes (vecLib)" ELSE "Yes (${BLAS})")
  deep_status("  Boost             :   Yes (ver. ${Boost_MAJOR_VERSION}.${Boost_MINOR_VERSION})")
  deep_status("  glog              :   Yes")
  deep_status("  gflags            :   Yes")
  deep_status("  protobuf          : " PROTOBUF_FOUND THEN "Yes (ver. ${PROTOBUF_VERSION})" ELSE "No" )
  if(USE_LMDB)
    deep_status("  lmdb              : " LMDB_FOUND THEN "Yes (ver. ${LMDB_VERSION})" ELSE "No")
  endif()
  if(USE_LEVELDB)
    deep_status("  LevelDB           : " LEVELDB_FOUND THEN  "Yes (ver. ${LEVELDB_VERSION})" ELSE "No")
    deep_status("  Snappy            : " SNAPPY_FOUND THEN "Yes (ver. ${Snappy_VERSION})" ELSE "No" )
  endif()
  if(USE_OPENCV)
    deep_status("  OpenCV            :   Yes (ver. ${OpenCV_VERSION})")
  endif()
  deep_status("  CUDA              : " HAVE_CUDA THEN "Yes (ver. ${CUDA_VERSION})" ELSE "No" )
  deep_status("")
  if(HAVE_CUDA)
    deep_status("NVIDIA CUDA:")
    deep_status("  Target GPU(s)     :   ${CUDA_ARCH_NAME}" )
    deep_status("  GPU arch(s)       :   ${NVCC_FLAGS_EXTRA_readable}")
    if(USE_CUDNN)
      deep_status("  cuDNN             : " HAVE_CUDNN THEN "Yes (ver. ${CUDNN_VERSION})" ELSE "Not found")
    else()
      deep_status("  cuDNN             :   Disabled")
    endif()
    deep_status("")
  endif()
  if(HAVE_PYTHON)
    deep_status("Python:")
    deep_status("  Interpreter       :" PYTHON_EXECUTABLE THEN "${PYTHON_EXECUTABLE} (ver. ${PYTHON_VERSION_STRING})" ELSE "No")
    deep_status("  Libraries         :" PYTHONLIBS_FOUND  THEN "${PYTHON_LIBRARIES} (ver ${PYTHONLIBS_VERSION_STRING})" ELSE "No")
    deep_status("  NumPy             :" NUMPY_FOUND  THEN "${NUMPY_INCLUDE_DIR} (ver ${NUMPY_VERSION})" ELSE "No")
    deep_status("")
  endif()
  if(BUILD_matlab)
    deep_status("Matlab:")
    deep_status("  Matlab            :" HAVE_MATLAB THEN "Yes (${Matlab_mex}, ${Matlab_mexext}" ELSE "No")
    deep_status("  Octave            :" Octave_compiler THEN  "Yes (${Octave_compiler})" ELSE "No")
    if(HAVE_MATLAB AND Octave_compiler)
      deep_status("  Build mex using   :   ${Matlab_build_mex_using}")
    endif()
    deep_status("")
  endif()
  if(BUILD_docs)
    deep_status("Documentaion:")
    deep_status("  Doxygen           :" DOXYGEN_FOUND THEN "${DOXYGEN_EXECUTABLE} (${DOXYGEN_VERSION})" ELSE "No")
    deep_status("  config_file       :   ${DOXYGEN_config_file}")

    deep_status("")
  endif()
  deep_status("Install:")
  deep_status("  Install path      :   ${CMAKE_INSTALL_PREFIX}")
  deep_status("")
endfunction()
